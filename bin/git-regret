#!/usr/bin/env perl
use v5.14;
use strict;
use warnings;
use autodie ':all';
use opts;
use Pod::Usage;
use List::MoreUtils qw(apply);
use Term::ANSIColor;

our $STATUS_MARKER = {
    new       => '⚡',
    commented => '-',
    merged    => ' ',
};

sub git (@) {
    warn "git @_\n" if $ENV{REGRET_DEBUG};
    if (defined wantarray) {
        open my $pipe, '-|', ( git => @_ );
        return apply { chomp } do {
            local $/ = wantarray ? $/ : undef;
            <$pipe>;
        };
    } else {
        system git => @_;
    }
}

my $command = shift or pod2usage();

my $git_user = git 'config', '--get', 'user.name';

given ($command) {
    when ('create') {
        opts
            my $from => { isa => 'Str', default => $git_user },
            my $to   => { isa => 'Str', required => 1 };

        my $commit = shift || 'HEAD';
        git 'notes', '--ref' => "review/request/$from/to/$to", 'append', $commit;
    }

    when ('comment') {
        opts
            my $from => { isa => 'Str', default => $git_user },
        my $commit = shift || 'HEAD';
        git 'notes', '--ref' => "review/comment/$from", 'edit', $commit;
    }

    when ('show-comment') {
        opts
            my $from => { isa => 'Str', default => $git_user },
        my $commit = shift || 'HEAD';
        git 'notes', '--ref' => "review/comment/$from", 'show', $commit;
    }

    when ('push') {
        git 'push', 'origin', "refs/notes/review/request/$git_user/to/*";
        git 'push', 'origin', "refs/notes/review/comment/$git_user";
    }

    when ('pull') {
        git 'remote', 'update';
        git 'fetch', 'origin', 'refs/notes/review/*:refs/notes/remote/review/*';

        for (git 'rev-parse', '--symbolic-full-name', '--glob=refs/notes/remote/review/*') {
            my ($name) = m<^refs/notes/remote/review/(.+)$> or do {
                warn "Could not parse: '$_'";
                next;
            };
            local $| = 1;
            print "$name ... ";
            git 'notes', "--ref=review/$name", 'merge', '-v', '-s' => 'union', "remote/review/$name";
        }
    }

    when ('list') {
        opts
            my $from => { isa => 'Str', default => '*' },
            my $to   => { isa => 'Str', default => $git_user },
            my $all;

        my @log = do {
            local $/ = "\0";
            my $branch = "notes/review/request/$from/to/$to";
            $branch = "--glob=$branch" if $from eq '*' || $to eq '*';
            git 'log', $branch, '--reverse', '--name-only', '--pretty=format:%h', '-z', '--diff-filter=A';
        };

        foreach my $log (@log) {
            next unless length $log;
            my ($note, $commit) = split /\n/, $log;
            my $note_name = git 'name-rev', '--name-only', $note;

            my $status = do {
                if (git('merge-base', 'master', $commit) eq $commit) {
                    'merged';
                } else {
                    my $note = git 'log', '-1', $commit, '--notes=review/comment/*', '--pretty=%N';
                    length $note ? 'commented' : 'new';
                }
            };

            if ($all) {
                # nop
            } else {
                next if $status eq 'merged';
            }

            my ($from, $to) = $note_name =~ m</review/request/(.+?)/to/([^~^]+)>;
            say colored([ 'bold', 'yellow' ], $STATUS_MARKER->{$status}),
                sprintf(' [%s] ', colored([ 'green' ], $note)),
                colored([ 'blue' ], sprintf '%14s ', git 'log', '-1', $note, '--pretty=format:%ar'),
                colored([ 'bold', 'blue' ], sprintf('%9s ⇢  %-9s', $from, $to)),
                colored([ 'yellow' ], $commit),
                ' ',
                git 'name-rev', '--name-only' => $commit;
        }
    }

    when ('show') {
        my $commit = shift or pod2usage();

        local *STDOUT = do {
            if (my $pager = $ENV{GIT_PAGER} // git 'config', '--get', 'core.pager') {
                open my $pipe, '|-', $pager;
                $pipe;
            } else {
                *STDOUT;
            }
        };

        for (
            git 'log', "master..$commit", '--notes=review/*',
                '--color', '--stat', '--decorate', '--date=relative', '--graph', '--simplify-by-decoration', '--sparse',
        ) {
            s<^(.+ )Notes \(review/comment/(.+?)\):><$1 . colored [ 'bold', 'blue' ], "✍ $2">e;
            s<^(.+ )Notes \(review/request/(.+?)/to/(.+?)\):><$1 . colored [ 'bold', 'blue' ], "$2 ⇢  $3">e;
            say;
        }
    }

    default {
        pod2usage();
    }
}

__END__

=head1 NAME

git-regret

=head1 USAGE

git-regret create [--to=user]

git-regret list [--from=user] [--to=user] [--all]

git-regret push

git-regret pull

git-regret show commit
